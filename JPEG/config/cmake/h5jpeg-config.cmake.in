#-----------------------------------------------------------------------------
# h5jpeg Config file for compiling against jpeg install directory
#-----------------------------------------------------------------------------
@PACKAGE_INIT@

string(TOUPPER @H5JPEG_PACKAGE@ H5JPEG_PACKAGE_NAME)

set (${H5JPEG_PACKAGE_NAME}_VALID_COMPONENTS static shared)

#-----------------------------------------------------------------------------
# User Options
#-----------------------------------------------------------------------------
set (${H5JPEG_PACKAGE_NAME}_BUILD_SHARED_LIBS    @BUILD_SHARED_LIBS@)
set (${H5JPEG_PACKAGE_NAME}_EXPORT_LIBRARIES     @H5JPEG_LIBRARIES_TO_EXPORT@)

#-----------------------------------------------------------------------------
# Directories
#-----------------------------------------------------------------------------
set (${H5JPEG_PACKAGE_NAME}_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")

set (${H5JPEG_PACKAGE_NAME}_SHARE_DIR "@PACKAGE_SHARE_INSTALL_DIR@")
set_and_check (${H5JPEG_PACKAGE_NAME}_BUILD_DIR "@PACKAGE_CURRENT_BUILD_DIR@")

#-----------------------------------------------------------------------------
# Version Strings
#-----------------------------------------------------------------------------
set (${H5JPEG_PACKAGE_NAME}_VERSION_STRING @H5JPEG_VERSION_STRING@)
set (${H5JPEG_PACKAGE_NAME}_VERSION_MAJOR  @H5JPEG_VERSION_MAJOR@)
set (${H5JPEG_PACKAGE_NAME}_VERSION_MINOR  @H5JPEG_VERSION_MINOR@)

#-----------------------------------------------------------------------------
# Don't include targets if this file is being picked up by another
# project which has already build h5jpeg as a subproject
#-----------------------------------------------------------------------------
if (NOT TARGET "@H5JPEG_PACKAGE@")
  include (@PACKAGE_SHARE_INSTALL_DIR@/@H5JPEG_PACKAGE@@H5JPEG_PACKAGE_EXT@-targets.cmake)
  set (H5JPEG_LIBRARIES "@H5JPEG_LIBRARIES_TO_EXPORT@")
endif ()

# Handle default component(static) :
if (NOT ${H5JPEG_PACKAGE_NAME}_FIND_COMPONENTS)
    set (${H5JPEG_PACKAGE_NAME}_FIND_COMPONENTS static)
    set (${H5JPEG_PACKAGE_NAME}_FIND_REQUIRED_static true)
endif ()

# Handle requested components:
list (REMOVE_DUPLICATES ${H5JPEG_PACKAGE_NAME}_FIND_COMPONENTS)
foreach (comp IN LISTS ${H5JPEG_PACKAGE_NAME}_FIND_COMPONENTS)
    list (FIND ${H5JPEG_PACKAGE_NAME}_EXPORT_LIBRARIES "@H5JPEG_LIB_CORENAME@-${comp}" HAVE_COMP)
    if (${HAVE_COMP} LESS 0)
      set (${H5JPEG_PACKAGE_NAME}_${comp}_FOUND 0)
    else ()
      set (${H5JPEG_PACKAGE_NAME}_${comp}_FOUND 1)
      string(TOUPPER ${H5JPEG_PACKAGE_NAME}_${comp}_LIBRARY COMP_LIBRARY)
      set (${COMP_LIBRARY} ${${COMP_LIBRARY}} @H5JPEG_LIB_CORENAME@-${comp})
    endif ()
endforeach ()

check_required_components (${H5JPEG_PACKAGE_NAME})
