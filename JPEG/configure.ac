# Copyright by The HDF Group. All rights reserved.

# This is the main configure file for the JPEG filter, a HDF5 plugin
# library that enables jpeg compression/decompression as a HDF5
# filter. This is part of the hdf5_plugin project.

# Ed Hartnett 1/16/19

# Initialize autoconf.
AC_PREREQ([2.71])
AC_INIT([H5JPEG],[0.1],[help@hdfgroup.org])
AC_CONFIG_HEADERS([jpeg_config.h])
AC_CONFIG_MACRO_DIR([m4])

AC_MSG_NOTICE([*** This is a configure for JPEG ***])

# Initialize automake.
AM_INIT_AUTOMAKE([foreign])

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

LT_INIT(dlopen)

AC_MSG_CHECKING([where to put HDF5 plugins])
HDF5_PLUGIN_DIR=${HDF5_PLUGIN_DIR-'/usr/local/hdf5/lib/plugin'}
AC_ARG_WITH([hdf5-plugin-dir],
            [AS_HELP_STRING([--with-hdf5-plugin-dir=<directory>],
                            [specify HDF5 plugin directory (defaults to /usr/local/hdf5/lib/plugin, or value of HDF5_PLUGIN_DIR, if set)])],
            [HDF5_PLUGIN_DIR=$with_hdf5_plugin_dir])
AC_MSG_RESULT($HDF5_PLUGIN_DIR)
AC_SUBST([HDF5_PLUGIN_DIR])

# Is the jpeg library present?
AC_CHECK_HEADERS([jpeglib.h], [], [AC_MSG_ERROR([jpeglib.h is required, set CPPFLAGS.])])
AC_CHECK_LIB([jpeg], [jpeg_abort], [], [AC_MSG_ERROR([libjpeg is required, set LDFLAGS.])])

# We need the math library
AC_CHECK_LIB([m], [floor], [], [AC_MSG_ERROR([Math library is required.])])

# We need the HDF5 headers and library.
AC_CHECK_HEADERS([hdf5.h], [], [AC_MSG_ERROR([hdf5.h is required, set CPPFLAGS.])])
AC_SEARCH_LIBS([H5Fflush], [hdf5dll hdf5], [], [AC_MSG_ERROR([libhdf5 is required, set LDFLAGS.])])

AC_CHECK_HEADERS([fcntl.h stdlib.h string.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_ERROR_AT_LINE
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([memset])

# Check which plugins to build, if no environmental variables are set, build
# all.
if test ! "$PLUGIN_H5JPEG"
then
  PLUGIN_H5JPEG=1
fi
AM_CONDITIONAL(H5JPEG, test "$PLUGIN_H5JPEG")

# These files will be generated by configure.
AC_CONFIG_FILES([Makefile
        example/Makefile
        src/Makefile])

# Output configure and all Makefile.in files.
AC_OUTPUT
