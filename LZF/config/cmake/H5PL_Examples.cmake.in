cmake_minimum_required(VERSION 3.12)
###############################################################################################################
# This script will build and run the examples from a folder
# Execute from a command line:
#     ctest -S H5PL_Examples.cmake,OPTION=VALUE -C Release -VV -O test.log
###############################################################################################################
# Minimum where plugins are installed within hdf5 library install, /usr/local/HDF_Group/HDF5/1.13/lib/plugin,
#                                     then <hdf5 root loaction> is /usr/local/HDF_Group/HDF5/1.13
#     ctest -S H5PL_Examples.cmake,HDF5INSTALLDIR=<hdf5 root location> -C Release -VV -O test.log
###############################################################################################################

set(CTEST_CMAKE_GENERATOR "@CMAKE_GENERATOR@")
if("@CMAKE_GENERATOR_TOOLSET@")
  set(CMAKE_GENERATOR_TOOLSET "@CMAKE_GENERATOR_TOOLSET@")
endif()
if("@CMAKE_GENERATOR_ARCHITECTURE@")
  set(CMAKE_GENERATOR_ARCHITECTURE "@CMAKE_GENERATOR_ARCHITECTURE@")
endif()
set(CTEST_DASHBOARD_ROOT ${CTEST_SCRIPT_DIRECTORY})

# handle input parameters to script.
#HDF5INSTALLDIR - HDF5 library root folder
#INSTALLDIR - HDF5 plugin root folder
#CTEST_CONFIGURATION_TYPE - Release, Debug, RelWithDebInfo
#CTEST_SOURCE_NAME - name of source folder; HDFPLExamples
#STATIC_ONLY - Default is YES
if(DEFINED CTEST_SCRIPT_ARG)
  # transform ctest script arguments of the form
  # script.ctest,var1=value1,var2=value2
  # to variables with the respective names set to the respective values
  string(REPLACE "," ";" script_args "${CTEST_SCRIPT_ARG}")
  foreach(current_var ${script_args})
    if("${current_var}" MATCHES "^([^=]+)=(.+)$")
      set("${CMAKE_MATCH_1}" "${CMAKE_MATCH_2}")
    endif()
  endforeach()
endif()

###################################################################
### Following Line is one of [Release, RelWithDebInfo, Debug] #####
set(CTEST_CONFIGURATION_TYPE "$ENV{CMAKE_CONFIG_TYPE}")
if(NOT DEFINED CTEST_CONFIGURATION_TYPE)
  set(CTEST_CONFIGURATION_TYPE "Release")
endif()
set(ADD_BUILD_OPTIONS "${ADD_BUILD_OPTIONS} -DCTEST_CONFIGURATION_TYPE:STRING=${CTEST_CONFIGURATION_TYPE}")
##################################################################

if(NOT DEFINED HDF5INSTALLDIR)
  set(HDF5INSTALLDIR "${CTEST_DASHBOARD_ROOT}/HDF_Group/HDF5/1.13")
endif()
if(NOT DEFINED INSTALLDIR)
  set(INSTALLDIR "${HDF5INSTALLDIR}")
endif()

if(NOT DEFINED CTEST_SOURCE_NAME)
  set(CTEST_SOURCE_NAME "HDFPLExamples")
endif()

if(NOT DEFINED STATIC_ONLY)
  set(STATICLIBRARIES "YES")
else(NOT DEFINED STATIC_ONLY)
  set(STATICLIBRARIES "NO")
endif()

if(NOT DEFINED HDF_LOCAL)
  set(CDASH_LOCAL "NO")
else()
  set(CDASH_LOCAL "YES")
endif()
if(NOT DEFINED CTEST_SITE)
  set(CTEST_SITE "local")
endif()
if(NOT DEFINED CTEST_BUILD_NAME)
  set(CTEST_BUILD_NAME "examples")
endif()
set(ADD_BUILD_OPTIONS "${ADD_BUILD_OPTIONS} -DSITE:STRING=${CTEST_SITE} -DBUILDNAME:STRING=${CTEST_BUILD_NAME}")

#TAR_SOURCE - name of tarfile
#if(NOT DEFINED TAR_SOURCE)
#  set(CTEST_USE_TAR_SOURCE "HDFPLExamples-1.13-Source")
#endif()

###############################################################################################################
if(WIN32)
  set(SITE_OS_NAME "Windows")
  set(ENV{HDF5_DIR} "${HDF5INSTALLDIR}/cmake")
  set(CTEST_BINARY_NAME ${CTEST_SOURCE_NAME}\\build)
  set(CTEST_SOURCE_DIRECTORY "${CTEST_DASHBOARD_ROOT}\\${CTEST_SOURCE_NAME}")
  set(CTEST_BINARY_DIRECTORY "${CTEST_DASHBOARD_ROOT}\\${CTEST_BINARY_NAME}")
  set(ENV{PATH} "$ENV{PATH};${HDF5INSTALLDIR}")
  set(HDF5_PLUGIN_ROOT "${INSTALLDIR}/lib/plugin")
else()
  set(ENV{HDF5_DIR} "${HDF5INSTALLDIR}/share/cmake")
  set(ENV{LD_LIBRARY_PATH} "${HDF5INSTALLDIR}/lib")
  set(CTEST_BINARY_NAME ${CTEST_SOURCE_NAME}/build)
  set(CTEST_SOURCE_DIRECTORY "${CTEST_DASHBOARD_ROOT}/${CTEST_SOURCE_NAME}")
  set(CTEST_BINARY_DIRECTORY "${CTEST_DASHBOARD_ROOT}/${CTEST_BINARY_NAME}")
  set(ENV{PATH} "$ENV{PATH}:${HDF5INSTALLDIR}")
  set(HDF5_PLUGIN_ROOT "${INSTALLDIR}/lib/plugin")
endif()
if(${CDASH_LOCAL})
  set(ADD_BUILD_OPTIONS "${ADD_BUILD_OPTIONS} -DCDASH_LOCAL:BOOL=ON")
endif()
set(ADD_BUILD_OPTIONS "${ADD_BUILD_OPTIONS} -DHDF5_PACKAGE_NAME:STRING=@HDF5_PACKAGE_NAME@")
set(ENV{HDF5_PLUGIN_PATH} "${HDF5_PLUGIN_ROOT}")

set(ADD_BUILD_OPTIONS "${ADD_BUILD_OPTIONS} -DDISABLE_H5PL_ENCODER:BOOL=@DISABLE_H5PL_ENCODER@")

###############################################################################################################
# For any comments please contact cdashhelp@hdfgroup.org
#
###############################################################################################################
if(WIN32)
  include(${CTEST_DASHBOARD_ROOT}\\HDFoptions.cmake)
  include(${CTEST_DASHBOARD_ROOT}\\CTestScript.cmake)
else()
  include(${CTEST_DASHBOARD_ROOT}/HDFoptions.cmake)
  include(${CTEST_DASHBOARD_ROOT}/CTestScript.cmake)
endif()
##############################################################################################################
message(STATUS "DONE")